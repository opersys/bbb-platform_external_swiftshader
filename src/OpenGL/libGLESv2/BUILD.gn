# Copyright 2016 The SwiftShader Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_libGLESv2_private_config") {
  if (is_clang) {
    cflags = [
      "-DLOG_TAG=\"swiftshader_libGLESv2\"",
      "-fno-operator-names",
      "-D__STDC_CONSTANT_MACROS",
      "-D__STDC_LIMIT_MACROS",
      "-DGL_API=",
      "-DGL_APICALL=",
      "-DGL_GLEXT_PROTOTYPES",
      "-Wno-sign-compare",
    ]

    if (!is_debug) {
      cflags += [
        "-DNDEBUG",
        "-DANGLE_DISABLE_TRACE",
      ]
    }
  }
}

shared_library("swiftshader_libGLESv2") {
  deps = [
    "../../Common:swiftshader_common",
    "../../Reactor:swiftshader_reactor",
    "../../Renderer:swiftshader_renderer",
    "../../OpenGL/common:swiftshader_opengl_common",
    "../../OpenGL/compiler:swiftshader_opengl_compiler",
    "../../Shader:swiftshader_shader",
    "../../Main:swiftshader_main",
    "../../../third_party/LLVM:swiftshader_llvm",
  ]

  sources = [
    "Buffer.cpp",
    "Context.cpp",
    "Device.cpp",
    "Fence.cpp",
    "Framebuffer.cpp",
    "IndexDataManager.cpp",
    "libGLESv2.cpp",
    "libGLESv3.cpp",
    "main.cpp",
    "Program.cpp",
    "Query.cpp",
    "Renderbuffer.cpp",
    "ResourceManager.cpp",
    "Shader.cpp",
    "Texture.cpp",
    "TransformFeedback.cpp",
    "utilities.cpp",
    "VertexArray.cpp",
    "VertexDataManager.cpp",
  ]

  configs += [
    ":swiftshader_libGLESv2_private_config",
  ]

  include_dirs = [
    "../../../include",
    "../..",
    "..",
  ]
}